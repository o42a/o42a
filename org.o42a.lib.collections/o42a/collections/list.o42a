~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Copyright (C) 2012-2014 Ruslan Lopatin

This Source Code Form is subject to the terms of the Mozilla Public
License, v. 2.0. If a copy of the MPL was not distributed with this
file, You can obtain one at http://mozilla.org/MPL/2.0/.
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

List ::=> collection
====================
~~~
An indexed list of elements.
~~~

@Indexed :=< #element type` * (
  Length = \size
)

<*Item by index> Item @indexed (
  <[Item index]!> Index = *
)

Iterate => * (
  ::Next index := ``0
  Next = * (
    $Index := next index->
    Next index += 1
    = List:: [index]
  )
)

<*Sublist> Sublist (
  ~~~
  Constructs a sublist by the given right-open list indices interval.
  ~~~
  <[From...) | to> From = *
  <[...To)!> To = *
)

<*Trailing sublist> Sublist (
  ~~~
  Constructs a trailing sublist by the given closed left-bounded
  list indices interval.
  ~~~
  <[From...-)!> From = *
)

<*Leading sublist> Sublist (
  ~~~
  Constructs a leading sublist by the given open right-bounded 
  list indices interval.
  ~~~
  <(-...To)!> To = *
)

<(-Full sublist...-)!> ~~ Returns a list itself when unbounded list indices
                       ~~ interval provided.
