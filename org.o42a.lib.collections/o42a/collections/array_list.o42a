~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Copyright (C) 2013 Ruslan Lopatin

This Source Code Form is subject to the terms of the Mozilla Public
License, v. 2.0. If a copy of the MPL was not distributed with this
file, You can obtain one at http://mozilla.org/MPL/2.0/.
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

Array list :=> mutable list
===========================
~~~
A <mutable list> storing its elements in array.
~~~

Data :=< variable (`array (`#element type))
~~ An array variable storing elements of this list.

Size = \data: length

Item => * (
  = Data [index]
  Set => * (
    Element assignment: {
      Data [index] = new value
    }
  )
)

<[[Items]]!> Data = *
~~~
Constructs a list by the given row.
~~~

<Of...> (
  ~~~
  Constructs a list by the given array.
  ~~~
  <[[Items]]!> Data = * ~~ Row expression argument.
  <[Array]!> Data = *   ~~ Array value argument. 
)

Add => * ({
  New data := (size + 1)~duplicates (`#element type)
  Data: copy _to [new data]
  New data [size] = new element
  Data = new data
})

Insert => * ({
  New data := (size + 1)~duplicates (`#element type)
  Data: copy [0 ... index) to [new data]
  New data [index] = new element
  Data: copy [index ...) to [new data] at [index + 1]
  Data = new data
})

Remove => * ({
  New data := (size - 1)~duplicates (`#element type)
  Data: copy [0 ... index) to [new data]
  Data: copy [index + 1 ...) to [new data] at [index]
  Data = new data
})
