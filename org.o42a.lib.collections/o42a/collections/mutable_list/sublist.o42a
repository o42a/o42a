~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Copyright (C) 2013,2014 Ruslan Lopatin

This Source Code Form is subject to the terms of the Mozilla Public
License, v. 2.0. If a copy of the MPL was not distributed with this
file, You can obtain one at http://mozilla.org/MPL/2.0/.
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

Sublist :=> #element type` /mutable list
========================================
~~~
A mutable sublist representing a slice of original list.
~~~

From := integer` link = 0
~~~
An index of the first list element represented by this sublist.

Zero by default.
~~~

To := integer` link = Mutable list:: size
~~~
A value one greater than index of the last list element
represented by this sublist.

Equals to the list size by default.
~~~

*Size (= to - from)

@Indexed (
  Item => * (
    Index < size? = Mutable list:: [from + index]
    @Assignable (
      Set => * ({
        Index < size? Mutable list:: [from + index] << new value
      })
    )
  )
)

Add => * (
  Mutable list:: insert [new element] at [to]
)

Insert => * (
  Index < size? Mutable list:: insert [new element] at [from + index]
)

Remove => * (
  Index < size? Mutable list:: remove [from + index]
)
