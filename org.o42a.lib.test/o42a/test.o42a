/*
    Test Framework
    Copyright (C) 2011 Ruslan Lopatin

    This file is part of o42a.

    o42a is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    o42a is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <http://www.gnu.org/licenses/>.
*/

Use object 'Print error' from 'Console'

/*
  Test signature.

  Inherit this object or create an adapter to it in public field. Then such
  field will be recognized as test.

  The 'run tests' directive searches for all tests in enclosing object
  and executes them.
 */
Test :=> void(

  /*
    Optional test name.
    
    Will be generated automatically if omitted.
  */
  Name := string(False)

  <*> (<*{}> (). = Void)

)

/* Failure message. */
Fail :=> void(
  <*> {
    <*> Print error(
      <*[]> Text = ()
      <*''> Text = ()
    )
    Print error(
      Text = "\n"
    )
    = Void
  }
)

/*
  Assertion.

  Usage syntax:
    Assert [condition] 'Error message'.
  where condition is arbitrary expression and error message is a message
  to report when condition fails.
 */
Assert :=> void(

  /*
    Condition which should be satisfied.

    Otherwise, an error reported.
   */
  Condition :=< `void

  /*
    Error message.

    A message reported when condition fails.
   */
  Message := `"Error"
  
  {
    --Condition? Fail [message]
    = Void
  }

  <*[] | error message*> Condition = ()
  <Error message> (
    <*''> Message = ()
    <*[]> Message = ()
  )
)
