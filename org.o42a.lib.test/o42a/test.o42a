~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Copyright (C) 2011-2014 Ruslan Lopatin

This Source Code Form is subject to the terms of the Mozilla Public
License, v. 2.0. If a copy of the MPL was not distributed with this
file, You can obtain one at http://mozilla.org/MPL/2.0/.
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Use object 'Print error' from 'Console'

Test :=> void (
~~~~~~~~~~~~~~~
Test signature.

Inherit this object or create an adapter to it in public field. Then such
field will be recognized as test.

The `run tests` directive searches for all tests in enclosing object
and executes them.
~~~

  Name := string (False)
  ~~~~~~~~~~~~~~~~~~~~~~
  Optional test name.
    
  Will be generated automatically if omitted.
  ~~~

  <{}>
)

Fail :=> void (
~~~~~~~~~~~~~~~
Failure message.
~~~
  <*> {
    Print error (Text = "ERROR: ")
    <*> Print error (
      <[]> Text = *
      <''> Text = ()
    )
    Print error _nl
  }
)

Assert :=> void (
~~~~~~~~~~~~~~~~~
Assertion.

Usage syntax:
    Assert [condition] 'Error message'.
where condition is arbitrary expression and error message is a message
to report when condition fails.
~~~

  Condition :=< `void
  ~~~~~~~~~~~~~~~~~~~
  Condition which should be satisfied.

  Otherwise, an error reported.
  ~~~

  Message := `"Error"
  ~~~~~~~~~~~~~~~~~~~
  Error message.

  A message reported when condition fails.
  ~~~
  
  --Condition->? Fail [message]

  <[] | error message> Condition = ()
  <:Error message> (
    <''> Message = ()
    <[]> Message = *
  )
)
