~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Tests
Copyright (C) 2011,2012 Ruslan Lopatin

This file is part of o42a.

o42a is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

o42a is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Use namespace 'Console'
Use namespace 'Test'

Floats := test
==============

Name = "Floats"

Print error '  ' [name] nl
Run tests

Unary conditions := test (
  Name = "Unary Conditions"
  Print error '    ' [name] nl

  Assert [+rt-float '3.13'] 'Unary float plus is not true'
  Assert [-rt-float '3.13'] 'Unary float minus is not true'
)

Equal := test (
  Name = "Equal"
  Print error '    ' [name] nl

  Assert [rt-float '7.25' == float '7.25'] 'Floats not equal'
  Assert [rt-float '7' == 7] 'Float and integer not equal'
)

Not equal := test (
  Name = "Not Equal"
  Print error '    ' [name] nl

  Assert [rt-float '5.1' <> rt-float '5.2'] 'Floats equal'
  Assert [--(rt-float '9.2' == rt-float '7.1')] 'Floats equal' 
  Assert [rt-float '7.4' <> 7] 'Float and integer equal'
)

Plus := test (
  Name = "Plus"
  Print error '    ' [name] nl

  Assert [+rt-float '5.1' == float '5.1'] 'Float plus failed'
)

Minus := test (
  Name = "Minus"
  Print error '    ' [name] nl

  Assert [-rt-float '5.1' == float '-5.1'] 'Float minus failed'
)

Arithmetics := test (
  Name = "Arithmetics"
  Print error '    ' [name] nl

  Assert [rt-float '5.5' + float '6.25' == float '11.75']
  _ 'Float addition failed'
  Assert [rt-float '6.5' - float '5' == float '1.5']
  _ 'Float subtraction failed'
  Assert [rt-float '5.25' * float '6' == float '31.5' ]
  _ 'Float multiplication failed'
  Assert [rt-float '7.5' / float '2.5' == float '3']
  _ 'Float division failed'
)

Mixed arithmetics := test (
  Name = "Mixed Arithmetics"
  Print error '    ' [name] nl

  Assert [rt-float '5.5' + 6 == float '11.5']
  _ 'Float and integer addition failed'
  Assert [rt-float '6.5' - 5 == float '1.5']
  _ 'Float and integer subtraction failed'
  Assert [rt-float '5.25' * 6 == float '31.5' ]
  _ 'Float and integer multiplication failed'
  Assert [rt-float '9.4' / 2 == float '4.7']
  _ 'Float and integer division failed'
)
