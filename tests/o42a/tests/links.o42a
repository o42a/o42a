~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Tests
Copyright (C) 2012 Ruslan Lopatin

This file is part of o42a.

o42a is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

o42a is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Use namespace 'Console'
Use namespace 'Test'

Links := test
=============

Name = "Link Tests"

Print error [name] nl
Run tests

Reference := test (
  Name = "Reference Link"
  Print error '  ' [name] nl

  Value := ``1
  Proxy := `value

  {
    Assert [proxy == 1] "Wrong initial reference link value"
    Value = 2
    Assert [proxy == 2] "Reference link value did not update properly"
  }
)

Expression := test (
  Name = "Expression Link"
  Print error '  ' [name] nl

  Value := ``1
  Proxy := `value * 10

  {
    Assert [proxy == 10] "Wrong initial expression link value"
    Value = 2
    Assert [proxy == 20] "Expression link value did not update properly"
  }
)

Target and body := test (
  Name = "Target and Body"
  Print error '  ' [name] nl
  
  Static link := link (`integer) [1]
  
  {
    Assert [static link`] "Static link body is false"
    Assert [static link] "Static link target is false"
    Assert [static link == 1] "Wrong static link target"
  }

  Runtime link := link (`integer) [rt-integer '2']
  
  {
    Assert [runtime link`] "Runtime link body is false"
    Assert [runtime link] "Runtime link target is false"
    Assert [runtime link == 2] "Wrong runtime link target"
  }  
)
